//querydsl 추가
buildscript {
	dependencies {
		classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10")
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.11'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id "com.diffplug.spotless" version "5.14.0"
	// rest docs
	id "org.asciidoctor.convert" version "1.5.10"
}

// java format git hooks auto pre-commit
spotless {
	java {
		googleJavaFormat()
	}
}

apply plugin: "com.ewerk.gradle.plugins.querydsl"

group = 'com.cobi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

// rest docs : snippetsDir
ext {
	snippetsDir = file('build/generated-snippets')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	runtimeOnly 'com.h2database:h2'
//	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'

	// tdd
	//restAssured
	testImplementation 'io.rest-assured:rest-assured:3.3.0'
	// junit 5 변경
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Restdocs
	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

	//jwt
	implementation 'io.jsonwebtoken:jjwt:0.9.1'

	//querydsl 추가
	implementation 'com.querydsl:querydsl-jpa'
	//querydsl 추가
	implementation 'com.querydsl:querydsl-apt'

	testImplementation 'org.springframework.security:spring-security-test'

}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}
//rest docs 추가
//setup asciidoctor
asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

// 여러 restdocs가 생기는 것을 방지
task createDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/asciidoc/html5/index.html")
	into file("src/main/resources/static/docs")
}

bootJar {
	dependsOn createDocument
	from("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

//querydsl 추가
//def querydslDir = 'src/main/generated'
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
	library = "com.querydsl:querydsl-apt"
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', querydslDir]
		}
	}
}
compileQuerydsl{
	options.annotationProcessorPath = configurations.querydsl
}
configurations {
	querydsl.extendsFrom compileClasspath
}